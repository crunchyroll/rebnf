ObjInner = string ":" Value .
Object = "{" [ ObjInner { "," ObjInner } ] "}" .

Array = "[" [ Value { "," Value } ] "]" .
Value = string | number | Object | Array | "true" | "false" | "null" .

posdigit = "1" … "9" .
digit = "0" | posdigit .

hex = digit | "A" … "F" | "a" … "f" .
uniesc = "u" hex hex hex hex .
esc = "\\" ( "\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t" | uniesc ) .
unichr = /* Any UNICODE character except " or \ or control character */ .
string = "\"" { unichr | esc } "\"" .

int = "0" | ( posdigit { digit } ) .
frac = "." digit { digit } .
exp = ( "e" | "E" ) [ "+" | "-" ] { digit } .
number = [ "-" ] int [ frac ] [ exp ] .
